buildscript {
  ext.kotlin_version = versions.kotlin
  apply from: './gradle/dependencies.gradle'
  repositories {
    google()
    jcenter()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:' + versions.gradlePlugin
    classpath 'com.diffplug.spotless:spotless-plugin-gradle:' + versions.spotlessPlugin
    classpath 'com.github.ben-manes:gradle-versions-plugin:' + versions.versionPlugin
    classpath 'com.novoda:bintray-release:' + versions.bintrayRelease
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:' + versions.kotlin
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }

  apply plugin: 'com.diffplug.gradle.spotless'
  spotless {
    java {
      target '**/*.java'
      trimTrailingWhitespace()
      removeUnusedImports()
      googleJavaFormat()
      endWithNewline()
      indentWithSpaces(2)
    }
  }
}

apply plugin: 'com.github.ben-manes.versions'
dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}